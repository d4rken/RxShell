buildscript {
    def versionMajor = 1
    def versionMinor = 0
    def versionPatch = 9

    ext.versions = [
            'versionCode'        : versionMajor * 10000 + versionMinor * 100 + versionPatch,
            'versionName'        : "${versionMajor}.${versionMinor}.${versionPatch}",
            'minSdk'             : 16,
            'targetSdk'          : 28,
            'compileSdk'         : 28,
            'sourceCompatibility': JavaVersion.VERSION_1_8,
            'targetCompatibility': JavaVersion.VERSION_1_8,
            'supportLibrary'     : '28.0.0',
            'buildTools'         : '28.0.3'
    ]

    def credentialsFile = new File(System.properties['user.home'], ".bintray/bintray.properties")
    if (credentialsFile.canRead()) {
        Properties props = new Properties()
        props.load(new FileInputStream(credentialsFile))
        ext.bintrayCredentials = [
                bintrayUser  : props['bintray.user'],
                bintrayApiKey: props['bintray.apikey']
        ]
    }

    ext.bintrayConfig = [
            bintrayRepo     : 'maven',
            bintrayName     : 'rxshell',
            publishedGroupId: 'eu.darken.rxshell',

            siteUrl         : 'https://github.com/d4rken/RxShell',
            issuesUrl       : 'https://github.com/d4rken/RxShell/issues',
            gitUrl          : 'https://github.com/d4rken/RxShell.git',

            libraryVersion  : versions.versionName,

            developerId     : 'darken',
            developerName   : 'Matthias Urhahn',
            developerEmail  : 'darken@darken.eu',

            licenseName     : 'Apache-2.0',
            licenseUrl      : 'https://github.com/d4rken/RxShell/blob/master/LICENSE',
            allLicenses     : ["Apache-2.0"]
    ]

    ext.deps = [
            'support'    : [
                    'annotations': "com.android.support:support-annotations:${versions.supportLibrary}",
                    'appcompat'  : "com.android.support:appcompat-v7:${versions.supportLibrary}"
            ],
            androidPlugin: 'com.android.tools.build:gradle:3.1.2',
            timber       : "com.jakewharton.timber:timber:4.7.1",
            rxJava       : "io.reactivex.rxjava2:rxjava:2.2.4",
            rxJavaReplay : "com.jakewharton.rx2:replaying-share:2.1.0",
            jUnit        : "junit:junit:4.12",
            mockito      : "org.mockito:mockito-core:2.8.9",
            awaitility   : "org.awaitility:awaitility:3.0.0",
    ]

    repositories {
        google()
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.1'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.0'
        //noinspection GradleDependency
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.8.2'
    }
}


apply plugin: "com.github.kt3k.coveralls"
apply plugin: 'java'

allprojects {
    apply plugin: 'jacoco'
    repositories {
        google()
        jcenter()
    }
}

subprojects { subProject ->
    def exclude = ['eu/darken/R*.class', '**/BuildConfig.class', '**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*']
    task jacocoTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest']) {
        reports {
            xml.enabled = true
            html.enabled = true
        }

        sourceDirectories = files(["${project.projectDir}/src/main/java"])
        classDirectories = files([fileTree(dir: "$project.buildDir/intermediates/classes/debug", excludes: exclude)])
        executionData = fileTree(dir: "$project.buildDir", includes: [
                "jacoco/testDebugUnitTest.exec"
        ])

        rootProject.coveralls.sourceDirs.addAll(sourceDirectories.flatten())
    }

    afterEvaluate {
        def isLibrary = subProject.plugins.hasPlugin('android-library')

        if (isLibrary) {
            subProject.apply plugin: "com.github.dcendents.android-maven"
            subProject.apply plugin: "com.jfrog.bintray"

            task javadoc(type: Javadoc) {
                source = android.sourceSets.main.java.srcDirs
                classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
            }

            if (subProject.hasProperty('bintrayCredentials')) {
                subProject.apply from: '../publish-to-bintray.gradle'
            }
        }
    }
}

def publishedProjects = subprojects.findAll { !it.path.contains('example') }
task jacocoRootReport(type: JacocoReport, group: 'Coverage reports') {
    description = 'Generates an aggregate report from all subprojects'

    dependsOn(publishedProjects.jacocoTestReport)

    additionalSourceDirs = files(publishedProjects.sourceSets.main.allSource.srcDirs)
    sourceDirectories = files(publishedProjects.jacocoTestReport.sourceDirectories)
    classDirectories = files(publishedProjects.jacocoTestReport.classDirectories)
    executionData = files(publishedProjects.jacocoTestReport.executionData)

    reports {
        html.enabled = true
        xml.enabled = true
    }
}

coveralls {
    jacocoReportPath = "${buildDir}/reports/jacoco/jacocoRootReport/jacocoRootReport.xml"
}

tasks.coveralls {
    group = 'Coverage reports'
    description = 'Uploads the aggregated coverage report to Coveralls'
    dependsOn jacocoRootReport
}
