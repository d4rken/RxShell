buildscript {
    def versionMajor = 1
    def versionMinor = 0
    def versionPatch = 0

    ext.versions = [
            'versionCode'        : versionMajor * 10000 + versionMinor * 100 + versionPatch,
            'versionName'        : "${versionMajor}.${versionMinor}.${versionPatch}",
            'minSdk'             : 16,
            'targetSdk'          : 27,
            'compileSdk'         : 27,
            'sourceCompatibility': JavaVersion.VERSION_1_8,
            'targetCompatibility': JavaVersion.VERSION_1_8,
            'supportLibrary'     : '27.0.2',
            'buildTools'         : '27.0.1'
    ]

    def credentialsFile = new File(System.properties['user.home'], ".bintray/bintray.properties")
    if (credentialsFile.canRead()) {
        Properties props = new Properties()
        props.load(new FileInputStream(credentialsFile))
        ext.bintrayCredentials = [
                bintrayUser  : props['bintray.user'],
                bintrayApiKey: props['bintray.apikey']
        ]
    }

    ext.bintrayConfig = [
            bintrayRepo     : 'maven',
            bintrayName     : 'rxshell',
            publishedGroupId: 'eu.darken.rxshell',

            siteUrl         : 'https://github.com/d4rken/RxShell',
            issuesUrl       : 'https://github.com/d4rken/RxShell/issues',
            gitUrl          : 'https://github.com/d4rken/RxShell.git',

            libraryVersion  : versions.versionName,

            developerId     : 'darken',
            developerName   : 'Matthias Urhahn',
            developerEmail  : 'darken@darken.eu',

            licenseName     : 'Apache-2.0',
            licenseUrl      : 'https://github.com/d4rken/RxShell/blob/master/LICENSE',
            allLicenses     : ["Apache-2.0"]
    ]

    ext.deps = [
            'support'    : [
                    'annotations': "com.android.support:support-annotations:${versions.supportLibrary}",
                    'appcompat'  : "com.android.support:appcompat-v7:${versions.supportLibrary}"
            ],
            androidPlugin: 'com.android.tools.build:gradle:3.0.1',
            timber       : "com.jakewharton.timber:timber:4.6.1",
            rxJava       : "io.reactivex.rxjava2:rxjava:2.1.6",
            rxJavaReplay : "com.jakewharton.rx2:replaying-share:2.0.1",
            jUnit        : "junit:junit:4.12",
            mockito      : "org.mockito:mockito-core:2.8.9",
            awaitility   : "org.awaitility:awaitility:3.0.0",
    ]

    repositories {
        google()
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.0'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.0'
        //noinspection GradleDependency
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.5.0-x'
    }
}

apply plugin: 'java'
apply plugin: "com.github.kt3k.coveralls"
apply plugin: 'jacoco'

allprojects {
    repositories {
        google()
        jcenter()
    }
}

subprojects { subProject ->
    def exclude = ['eu/darken/R*.class', '**/BuildConfig.class', '**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*']
    apply plugin: 'jacoco'
    task jacocoTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest']) {
        group = 'Coverage reports'
        description = 'Generates a test coverage report for a project'

        reports {
            xml.enabled = true
            html.enabled = true
        }

        def debugTree = fileTree(dir: "$project.buildDir/intermediates/classes/debug", excludes: exclude)
        def mainSrc = "${project.projectDir}/src/main/java"

        sourceDirectories = files([mainSrc])
        classDirectories = files([debugTree])
        executionData = fileTree(dir: "$project.buildDir", includes: [
                "jacoco/testDebugUnitTest.exec",
                "outputs/code-coverage/connected/*coverage.ec"
        ])
    }

    afterEvaluate {
        def isLibrary = subProject.plugins.hasPlugin('android-library')

        if (isLibrary) {
            subProject.apply plugin: "com.github.dcendents.android-maven"
            subProject.apply plugin: "com.jfrog.bintray"
            subProject.apply plugin: "jacoco"

            task javadoc(type: Javadoc) {
                source = android.sourceSets.main.java.srcDirs
                classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
            }

            if (subProject.hasProperty('bintrayCredentials')) {
                subProject.apply from: '../publish-to-bintray.gradle'
            }
        }
    }
}
def publishedProjects = subprojects.findAll { it.path != ':example' }

task jacocoMerge(type: JacocoMerge) {
    publishedProjects.each { subproject ->
        executionData subproject.tasks.withType(Test)
    }
    doFirst {
        executionData = files(executionData.findAll { it.exists() })
    }
}

task jacocoRootReport(type: JacocoReport) {
    description = 'Generates an aggregate report from all subprojects'
    dependsOn publishedProjects.jacocoTestReport, jacocoMerge

    additionalSourceDirs = files(publishedProjects.sourceSets.main.allSource.srcDirs)
    sourceDirectories = files(publishedProjects.sourceSets.main.allSource.srcDirs)
    classDirectories = files(publishedProjects.sourceSets.main.output)
    executionData jacocoMerge.destinationFile
    reports {
        html.enabled = true
        xml.enabled = true
        csv.enabled = false
    }
}

coveralls {
    sourceDirs = publishedProjects.sourceSets.main.allSource.srcDirs.flatten()
    jacocoReportPath = "${buildDir}/reports/jacoco/jacocoRootReport/jacocoRootReport.xml"
}

tasks.coveralls {
    group = 'Coverage reports'
    description = 'Uploads the aggregated coverage report to Coveralls'

    dependsOn jacocoRootReport
    onlyIf { System.env.'CI' && !JavaVersion.current().isJava9Compatible() }
}
